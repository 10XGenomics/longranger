#
# Copyright (c) 2014 10X Genomics, Inc. All rights reserved.
#
@include "_structvar_caller_stages.mro"

pipeline _STRUCTVAR_CALLER_FRAG(
    in  bam        input                  "duplicate-marked, sorted aligned bam file",
    in  string     sample_id,
    in  string     sex,
    in  bool       is_germline,
    in  tsv        blacklist              "barcode blacklist",
    in  bed        targets,
    in  int        target_extend,
    in  string     restrict_locus,
    in  bedpe      gt_variants,
    in  int        nx,
    in  int        window_size            "window size for BC overlap",
    in  int        step                   "step for windowing",
    in  int        min_reads              "min reads in a window - merge windows otherwise",
    in  float      max_bcs_to_call,
    in  string     overlap_test           "type of test to use in DETECT_OVERLAPS",
    in  float      frag_size_prc          "percentile of fragment size distribution - won't call smaller events",
    in  int        max_merge_dist,
    in  float      min_bc_overlap,
    in  bool       read1_only,
    in  bool       slide,
    in  int        min_mapq               "minimum mapq for read to be considered",
    in  int        min_overlap,
    in  int        sv_min_qv              "maximum output p-value",
    in  int        sv_min_call_qv_wgs,
    in  int        sv_min_call_qv_target,
    in  int        min_call_dist,
    in  int        min_read_support,
    in  string     reference_path,
    in  bed        sv_blacklist_regions   "genome region blacklist for SV calling",
    in  bedpe      seg_dups,
    in  int        min_dist_from_black,
    in  float      max_frac_black,
    in  int        seg_dup_min_dist,
    in  int[]      detect_dists,
    in  int[]      target_dists,
    in  int        min_sv_len             "restrict ground truth to svs of at least this length",
    in  json       insert_sizes,
    in  json       basic_summary,
    in  h5         fragments,
    in  json       fragment_histogram,
    in  tsv.gz     fragment_phasing,
    in  h5         barcodes,
    in  json       coverage_summary,
    in  int        min_reads_to_call,
    in  int        min_lr_to_call,
    in  float      max_logp,
    in  int        grid_len,
    in  float      p_ov_mol,
    in  int        break_ext              "extention of breakpoint candidates in CALL_STRUCTVARS_FRAG",
    in  int        min_frag_size          "min (observed) size of fragment to consider in CALL_STRUCTVARS_FRAG",
    in  int        min_reads_per_frag     "min num of reads on a fragment to consider in CALL_STRUCTVARS_FRAG",
    in  float      max_frac_low_mapq,
    out bedpe      sv_calls               "called variants file",
    out tsv        sv_call_details,
    out bedpe      sv_candidates,
    out vcf.gz     svs,
    out vcf.gz.tbi svs_index              "index"                   "svs.vcf.gz.tbi",
    out json       summary,
    out pickle     inv_bc_map,
    out pickle     bc_counts,
    out pickle     win_counts,
)
{
    call volatile COUNT_READS_BCS(
        input          = self.input,
        sex            = self.sex,
        reference_path = self.reference_path,
        blacklist      = self.blacklist,
        barcodes       = self.barcodes,
        inv_bc_map     = null,
        targets        = self.targets,
        target_extend  = self.target_extend,
        restrict_locus = self.restrict_locus,
        window_size    = self.window_size,
        step           = self.step,
        min_reads      = self.min_reads,
        max_merge_dist = self.max_merge_dist,
        min_mapq       = self.min_mapq,
        read1_only     = self.read1_only,
        no_split       = false,
        slide          = self.slide,
    )

    call volatile DETECT_OVERLAPS(
        possorted_bam      = self.input,
        fragments          = self.fragments,
        fragment_histogram = self.fragment_histogram,
        bc_pos_mats        = COUNT_READS_BCS.bc_pos_mats,
        inv_bc_map         = COUNT_READS_BCS.inv_bc_map,
        bc_counts          = COUNT_READS_BCS.bc_counts,
        win_counts         = COUNT_READS_BCS.win_counts,
        loci               = COUNT_READS_BCS.loci,
        nx                 = self.nx,
        min_overlap        = self.min_overlap,
        min_call_dist      = self.min_call_dist,
        max_call_dist      = 1000000000,
        max_bcs_to_call    = self.max_bcs_to_call,
        max_logp           = self.max_logp,
        test               = self.overlap_test,
    )

    call CALL_STRUCTVARS_FRAG(
        input              = self.input,
        min_mapq           = self.min_mapq,
        overlap_loci       = DETECT_OVERLAPS.overlap_loci,
        sv_min_qv          = self.sv_min_qv,
        min_call_dist      = self.min_call_dist,
        fragments          = self.fragments,
        fragment_histogram = self.fragment_histogram,
        fragment_phasing   = self.fragment_phasing,
        barcode_blacklist  = self.blacklist,
        coverage           = self.coverage_summary,
        targets            = self.targets,
        frag_size_prc      = self.frag_size_prc,
        grid_len           = self.grid_len,
        p_ov_mol           = self.p_ov_mol,
        break_ext          = self.break_ext,
        min_frag_size      = self.min_frag_size,
        min_reads_per_frag = self.min_reads_per_frag,
    )

    call volatile GET_READPAIR_EVIDENCE(
        input             = self.input,
        sv_variants       = CALL_STRUCTVARS_FRAG.sv_variants,
        break_extend      = self.window_size,
        min_mapq          = self.min_mapq,
        min_reads_to_call = self.min_reads_to_call,
        min_lr_to_call    = self.min_lr_to_call,
        rp_lr_multiplier  = 0,
        insert_sizes      = self.insert_sizes,
        basic_summary     = self.basic_summary,
        best_only         = true,
    )

    call MERGE_SV_CALLS(
        sv_variants1     = GET_READPAIR_EVIDENCE.sv_variants,
        sv_variants2     = null,
        max_dist         = 100000,
        min_bc_overlap   = self.min_bc_overlap,
        min_frac_overlap = null,
        merge_dist       = true,
    )

    call FILTER_LOW_MAPQ(
        possorted_bam     = self.input,
        sv_calls          = MERGE_SV_CALLS.sv_variants,
        max_frac_low_mapq = self.max_frac_low_mapq,
    )

    call ANALYZE_SV_CALLS(
        variants             = FILTER_LOW_MAPQ.sv_calls,
        keep_filters         = false,
        sample_id            = self.sample_id,
        gt_variants          = self.gt_variants,
        call_summary         = CALL_STRUCTVARS_FRAG.summary,
        coverage             = self.coverage_summary,
        min_call_qv_wgs      = self.sv_min_call_qv_wgs,
        min_call_qv_target   = self.sv_min_call_qv_target,
        min_read_support     = self.min_read_support,
        reference_path       = self.reference_path,
        sv_blacklist_regions = self.sv_blacklist_regions,
        seg_dups             = self.seg_dups,
        min_dist_from_black  = self.min_dist_from_black,
        max_frac_black       = self.max_frac_black,
        seg_dup_min_dist     = self.seg_dup_min_dist,
        detect_dists         = self.detect_dists,
        min_rel_overlap      = null,
        min_allelic_frac     = null,
        targets              = self.targets,
        target_dists         = self.target_dists,
        min_sv_len           = self.min_sv_len,
        is_germline          = self.is_germline,
        max_bc_cov_factor    = 4,
        blacklist_mode       = "sv",
        segdup_mode          = "sv",
    )

    return (
        sv_calls        = ANALYZE_SV_CALLS.sv_calls,
        sv_candidates   = ANALYZE_SV_CALLS.sv_candidates,
        svs             = ANALYZE_SV_CALLS.svs,
        svs_index       = ANALYZE_SV_CALLS.svs_index,
        sv_call_details = ANALYZE_SV_CALLS.call_tsv,
        summary         = ANALYZE_SV_CALLS.summary,
        inv_bc_map      = COUNT_READS_BCS.inv_bc_map,
        bc_counts       = COUNT_READS_BCS.bc_counts,
        win_counts      = COUNT_READS_BCS.win_counts,
    )
}
