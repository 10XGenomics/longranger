#
# Copyright (c) 2014 10X Genomics, Inc. All rights reserved.
#
@include "_aligner.mro"
@include "_preflight_stages.mro"
@include "_reporter.mro"
@include "_snpindel_phaser.mro"
@include "_structvar_caller.mro"
@include "_short_structvar_caller.mro"
@include "_loupe_stages.mro"

pipeline PHASER_SVCALLER_BWA(
    in  string     fastq_mode                  "configuration of the input fastqs",
    in  string     sample_id,
    in  bool       is_germline,
    in  map[]      sample_def,
    in  map        downsample,
    in  string     sex,
    in  vcf.gz     comparison_vcf,
    in  string     reference_path,
    in  bed        targets,
    in  map        vc_regions_of_interest,
    in  map        cov_regions_of_interest,
    in  string     restrict_locus              "locus to restrict phasing to",
    in  bool       exclude_non_bc_reads,
    in  int        trim_length,
    in  string     barcode_whitelist,
    in  bed        confident_regions,
    in  string     vc_precalled,
    in  string     vc_mode                     "'freebayes' or 'gatk:/path/to/GenomeAnalysisTK.jar'",
    in  vcf        vc_ground_truth,
    in  bam        validation_bam,
    in  int        sv_min_qv,
    in  int        sv_min_call_qv_wgs,
    in  int        sv_min_call_qv_target,
    in  int        sv_min_read_support,
    in  bedpe      sv_ground_truth,
    in  bedpe      del_ground_truth,
    in  int        vc_max_coverage,
    in  string     sample_desc,
    in  bool       noloupe,
    in  csv        p1_genes_list,
    out bam        phased_possorted_bam,
    out bam.bai    phased_possorted_bam_index,
    out bam        possorted_bam,
    out bam.bai    possorted_bam_index,
    out bam        bcsorted_bam,
    out json       duplicate_summary,
    out json       basic_summary,
    out h5         barcode_counts,
    out json       insert_sizes,
    out json       target_dists,
    out json       mapq_counts,
    out json       coverage_results,
    out json       attach_phasing_results,
    out h5         coverage_details,
    out h5         target_coverage,
    out csv        coverage_csv,
    out json       filter_barcodes_results,
    out json       length_mass_results,
    out h5         fragments,
    out h5         barcodes,
    out tsv.gz     fragment_phasing,
    out bed        high_coverage_excluded_bed,
    out json       variant_results,
    out vcf.gz     phased_variants,
    out vcf.gz.tbi phased_variants_index,
    out h5         variants_h5,
    out csv        gene_stats,
    out bedpe      sv_calls,
    out bedpe      sv_candidates,
    out vcf.gz     svs,
    out vcf.gz.tbi svs_index,
    out tsv        sv_gt_results,
    out json       sv_summary,
    out bedpe      short_del_calls,
    out json       short_del_summary,
    out vcf.gz     short_svs,
    out vcf.gz.tbi short_svs_index,
    out json       summary,
    out csv        summary_cs,
    out pickle     inv_bc_map,
    out pickle     bc_counts,
    out pickle     win_counts,
    out loupe      loupe,
    out tsv        barcode_blacklist,
    out json       alarms,
    out txt        alarms_summary,
    out json       lot_info,
    out json       downsample_info,
    out json       barcode_count,
    out json       single_partition_results,
    out json       fragment_histogram,
    out json       barcode_histogram,
)
{
    call local preflight PHASER_SVCALLER_PREFLIGHT_LOCAL(
        sample_id         = self.sample_id,
        sample_def        = self.sample_def,
        sex               = self.sex,
        targets           = self.targets,
        target_blacklist  = null,
        restrict_locus    = self.restrict_locus,
        vc_precalled      = self.vc_precalled,
        vc_mode           = self.vc_mode,
        reference_path    = self.reference_path,
        vc_ground_truth   = self.vc_ground_truth,
        sv_min_qv         = self.sv_min_qv,
        sv_ground_truth   = self.sv_ground_truth,
        check_executables = false,
    )

    call preflight PHASER_SVCALLER_PREFLIGHT(
        sample_id         = self.sample_id,
        sample_def        = self.sample_def,
        sex               = self.sex,
        targets           = self.targets,
        target_blacklist  = null,
        restrict_locus    = self.restrict_locus,
        vc_precalled      = self.vc_precalled,
        vc_mode           = self.vc_mode,
        reference_path    = self.reference_path,
        vc_ground_truth   = self.vc_ground_truth,
        sv_min_qv         = self.sv_min_qv,
        sv_ground_truth   = self.sv_ground_truth,
        check_executables = true,
    )

    call _ALIGNER(
        sample_id            = self.sample_id,
        fastq_mode           = self.fastq_mode,
        sample_def           = self.sample_def,
        exclude_non_bc_reads = self.exclude_non_bc_reads,
        trim_length          = self.trim_length,
        reference_path       = self.reference_path,
        barcode_whitelist    = self.barcode_whitelist,
        targets              = self.targets,
        downsample           = self.downsample,
    )

    call _REPORTER(
        sex                      = self.sex,
        vc_max_coverage          = self.vc_max_coverage,
        bcsorted_bam             = _ALIGNER.bcsorted_bam,
        possorted_bam            = _ALIGNER.possorted_bam,
        reference_path           = self.reference_path,
        targets                  = self.targets,
        confident_regions        = self.confident_regions,
        barcode_whitelist        = self.barcode_whitelist,
        restrict_locus           = self.restrict_locus,
        lot_info                 = _ALIGNER.lot_info,
        coverage_rois            = self.cov_regions_of_interest,
        barcodes                 = _ALIGNER.barcodes,
        fragments                = _ALIGNER.fragments,
        fragment_histogram       = _ALIGNER.fragment_histogram,
        barcode_histogram        = _ALIGNER.barcode_histogram,
        single_partition_results = _ALIGNER.single_partition_results,
    )

    call _SNPINDEL_PHASER(
        comparison_vcf             = self.comparison_vcf,
        coverage                   = _REPORTER.coverage_details,
        high_coverage_excluded_bed = _REPORTER.high_coverage_excluded_bed,
        sex                        = _REPORTER.sex,
        reference_path             = self.reference_path,
        restrict_locus             = self.restrict_locus,
        targets                    = self.targets,
        vc_regions_of_interest     = self.vc_regions_of_interest,
        input                      = _ALIGNER.possorted_bam,
        fragments                  = _REPORTER.fragments,
        vc_precalled               = self.vc_precalled,
        mode                       = self.vc_mode,
        ground_truth               = self.vc_ground_truth,
        validation_bam             = self.validation_bam,
        p1_genes_list              = self.p1_genes_list,
        bc_mix_prob                = 0.001,
        min_var_hap_conf           = 0.995,
        min_junction_hap_conf      = 0.995,
        hap_block_size             = 35,
        hap_block_buffer_size      = 5,
        max_reassign_rounds        = 4,
        min_mapq_attach_bc         = 30,
        chunk_stitching_overlap    = 100000,
    )

    call _STRUCTVAR_CALLER(
        is_germline           = self.is_germline,
        input                 = _SNPINDEL_PHASER.phased_possorted_bam,
        sample_id             = self.sample_id,
        sex                   = _REPORTER.sex,
        barcode_whitelist     = self.barcode_whitelist,
        blacklist             = _REPORTER.barcode_blacklist,
        targets               = self.targets,
        target_extend         = null,
        restrict_locus        = null,
        gt_variants           = self.sv_ground_truth,
        nx                    = 90,
        window_size           = 20000,
        step                  = 20000,
        min_reads             = 500,
        max_bcs_to_call       = 0.999,
        max_merge_dist        = 10000,
        read1_only            = true,
        slide                 = true,
        min_mapq              = 60,
        min_overlap           = 5,
        sv_min_qv             = self.sv_min_qv,
        sv_min_call_qv_wgs    = self.sv_min_call_qv_wgs,
        sv_min_call_qv_target = self.sv_min_call_qv_target,
        min_call_dist         = 35000,
        seg_dup_min_dist      = 20000,
        min_read_support      = self.sv_min_read_support,
        reference_path        = self.reference_path,
        min_dist_from_black   = 10000,
        sv_blacklist_regions  = null,
        seg_dups              = null,
        max_frac_black        = 0.5,
        detect_dists          = [
            1000,
            10000
        ],
        target_dists          = [
            12000,
            25000,
            50000
        ],
        min_sv_len            = 30000,
        insert_sizes          = _REPORTER.insert_sizes,
        fragments             = _REPORTER.fragments,
        fragment_histogram    = _ALIGNER.fragment_histogram,
        barcodes              = _REPORTER.barcodes,
        coverage_summary      = _REPORTER.coverage_results,
        coverage              = _REPORTER.coverage_details,
        low_depth_min_len     = 30000,
        min_rp_lr_to_call     = 0,
        rp_lr_multiplier      = 0,
        min_frac_overlap      = 0.8,
        min_reads_to_call     = 2,
        rp_break_ext          = 20000,
        min_rel_depth         = 20,
        max_clipped_frac      = 0.1,
        basic_summary         = _REPORTER.basic_summary,
        fragment_phasing      = _SNPINDEL_PHASER.fragment_phasing,
        coverage_csv          = _REPORTER.coverage_csv,
        p_ov_mol              = 1e-10,
        grid_len              = 1000,
        max_cand_breaks       = 100,
        overlap_test          = "EXP_COUNT3",
    )

    call _SHORT_STRUCTVAR_CALLER(
        sample_id          = self.sample_id,
        possorted_bam      = _SNPINDEL_PHASER.phased_possorted_bam,
        sex                = _REPORTER.sex,
        barcode_whitelist  = self.barcode_whitelist,
        reference_path     = self.reference_path,
        gt_variants        = self.del_ground_truth,
        basic_summary      = _REPORTER.basic_summary,
        insert_sizes       = _REPORTER.insert_sizes,
        coverage_csv       = _REPORTER.coverage_csv,
        coverage_details   = _REPORTER.coverage_details,
        fragment_phasing   = _SNPINDEL_PHASER.fragment_phasing,
        merge_range_factor = 1,
        min_rp_del_len     = 300,
        min_del_len        = 50,
        max_sv_len         = 30000,
        min_mapq           = 30,
        min_reads_to_call  = 3,
        min_call_qv        = 2,
        max_bc_coverage    = 600,
        min_lr             = 4,
        break_pad          = 500,
        detect_dists       = [0],
        min_rel_overlap    = 0.5,
        min_allelic_frac   = null,
        em_iters           = 5,
        del_hmm_trans_prob = 1e-05,
        min_kmer_obs       = 3,
        het_read_prob      = 0.97,
        coverage_results   = _REPORTER.coverage_results,
    )

    call SUMMARIZE_REPORTS(
        sample_id                = self.sample_id,
        reference_path           = self.reference_path,
        targets                  = self.targets,
        trim_length              = self.trim_length,
        duplicate_summary        = _ALIGNER.duplicate_summary,
        basic_results            = _REPORTER.basic_summary,
        barcode_counts           = _REPORTER.barcode_counts,
        filter_barcodes_results  = _REPORTER.filter_barcodes_results,
        coverage_results         = _REPORTER.coverage_results,
        coverage_details         = _REPORTER.coverage_details,
        variant_results          = _SNPINDEL_PHASER.results,
        sv_results               = _STRUCTVAR_CALLER.summary,
        sv_min_call_qv_wgs       = self.sv_min_call_qv_wgs,
        sv_min_call_qv_target    = self.sv_min_call_qv_target,
        short_del_results        = _SHORT_STRUCTVAR_CALLER.summary,
        single_partition_results = _ALIGNER.single_partition_results,
        length_mass_results      = _REPORTER.length_mass_results,
        bam_file                 = _SNPINDEL_PHASER.phased_possorted_bam,
        lot_info                 = _REPORTER.lot_info,
        downsample_info          = _ALIGNER.downsample_info,
    )

    call LOUPE_PREPROCESS(
        reference_path          = self.reference_path,
        input_vcf               = _SNPINDEL_PHASER.phased_variants,
        summarize_output        = SUMMARIZE_REPORTS.summary,
        single_partition_data   = _ALIGNER.single_partition_results,
        coverage_data           = _REPORTER.coverage_results,
        fragment_histogram      = _ALIGNER.fragment_histogram,
        phasing_quality_data    = _SNPINDEL_PHASER.results,
        sorted_deduplicated_bam = _SNPINDEL_PHASER.phased_possorted_bam,
        structvar_data          = _STRUCTVAR_CALLER.sv_candidates,
        shortstructvar_data     = _SHORT_STRUCTVAR_CALLER.sv_calls,
        bkpt_details            = null,
        alarms                  = SUMMARIZE_REPORTS.alarms,
        targets                 = self.targets,
        fragments               = _SNPINDEL_PHASER.fragment_phasing,
        length_mass_data        = _REPORTER.length_mass_results,
        sample_desc             = self.sample_desc,
        noloupe                 = self.noloupe,
    )

    return (
        duplicate_summary          = _ALIGNER.duplicate_summary,
        phased_possorted_bam       = _SNPINDEL_PHASER.phased_possorted_bam,
        phased_possorted_bam_index = _SNPINDEL_PHASER.phased_possorted_bam_index,
        possorted_bam              = _ALIGNER.possorted_bam,
        possorted_bam_index        = _ALIGNER.possorted_bam_index,
        bcsorted_bam               = _ALIGNER.bcsorted_bam,
        variant_results            = _SNPINDEL_PHASER.results,
        phased_variants            = _SNPINDEL_PHASER.phased_variants,
        phased_variants_index      = _SNPINDEL_PHASER.phased_variants_index,
        variants_h5                = _SNPINDEL_PHASER.variants_h5,
        gene_stats                 = _SNPINDEL_PHASER.gene_stats,
        basic_summary              = _REPORTER.basic_summary,
        barcode_counts             = _REPORTER.barcode_counts,
        insert_sizes               = _REPORTER.insert_sizes,
        target_dists               = _REPORTER.target_dists,
        mapq_counts                = _REPORTER.mapq_counts,
        coverage_results           = _REPORTER.coverage_results,
        coverage_details           = _REPORTER.coverage_details,
        target_coverage            = _REPORTER.target_coverage,
        coverage_csv               = _REPORTER.coverage_csv,
        filter_barcodes_results    = _REPORTER.filter_barcodes_results,
        length_mass_results        = _REPORTER.length_mass_results,
        high_coverage_excluded_bed = _REPORTER.high_coverage_excluded_bed,
        fragments                  = _REPORTER.fragments,
        barcodes                   = _REPORTER.barcodes,
        fragment_phasing           = _SNPINDEL_PHASER.fragment_phasing,
        sv_calls                   = _STRUCTVAR_CALLER.sv_calls,
        sv_candidates              = _STRUCTVAR_CALLER.sv_candidates,
        svs                        = _STRUCTVAR_CALLER.svs,
        svs_index                  = _STRUCTVAR_CALLER.svs_index,
        sv_gt_results              = _STRUCTVAR_CALLER.sv_call_details,
        sv_summary                 = _STRUCTVAR_CALLER.summary,
        short_del_calls            = _SHORT_STRUCTVAR_CALLER.sv_calls,
        short_del_summary          = _SHORT_STRUCTVAR_CALLER.summary,
        short_svs                  = _SHORT_STRUCTVAR_CALLER.svs,
        short_svs_index            = _SHORT_STRUCTVAR_CALLER.svs_index,
        inv_bc_map                 = _STRUCTVAR_CALLER.inv_bc_map,
        bc_counts                  = _STRUCTVAR_CALLER.bc_counts,
        win_counts                 = _STRUCTVAR_CALLER.win_counts,
        summary                    = SUMMARIZE_REPORTS.summary,
        summary_cs                 = SUMMARIZE_REPORTS.summary_cs,
        loupe                      = LOUPE_PREPROCESS.output_for_loupe,
        barcode_blacklist          = _REPORTER.barcode_blacklist,
        alarms                     = SUMMARIZE_REPORTS.alarms,
        alarms_summary             = SUMMARIZE_REPORTS.alarms_summary,
        attach_phasing_results     = _SNPINDEL_PHASER.summary,
        lot_info                   = _REPORTER.lot_info,
        downsample_info            = _ALIGNER.downsample_info,
        barcode_count              = _ALIGNER.barcode_count,
        single_partition_results   = _ALIGNER.single_partition_results,
        fragment_histogram         = _ALIGNER.fragment_histogram,
        barcode_histogram          = _ALIGNER.barcode_histogram,
    )
}
